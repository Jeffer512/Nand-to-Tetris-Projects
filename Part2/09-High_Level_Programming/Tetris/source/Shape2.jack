//    __ __
//   |a_|b_|
//   |c_|d_|

class Shape2 {

    static Tetris instance;
    static Shape2 shape;
    field int counter, size, leftWall, rightWall, bottom, wait;
    field Square a, b, c, d;

    /** Constructs a new figure of class Shape2 with the given parameters. */
    constructor Shape2 new(int left, int right, int squareSize, boolean outline, int x0, int floor, int w) {
        let instance = Tetris.getInstance();
        let size = squareSize;
        let leftWall = left;
        let rightWall = right - size + 1;
        let counter = 0;
        let bottom = floor;
        let wait = w;

        let a = Square.new(x0, 0, size, leftWall, outline);
        let b = Square.new(x0 + size, 0, size, leftWall, outline);
        let c = Square.new(x0, size, size, leftWall, outline);
        let d = Square.new(x0 + size, size, size, leftWall, outline);

        return this;
    }

    /** Constructs a new figure of class Shape2 and stores it. If the playes lost returns 2, else call move() method.*/
    function int start(int leftWall, int rightWall, int size, bool outline, int x0, int bottom, int wait) {
        var int key;
        let shape = Shape2.new(leftWall, rightWall, size, outline, x0, bottom, wait);
        if (shape.checkLose() = 1) {
            return 2;
        }
        let key = shape.move();
        do shape.dispose();
        return key;
    }

    /** Handles input from the user that controls the figure.
    Starts moving the figure down until it reaches the bottom of the screen or the top of another figure. */ 
    method int move() {
        var int key, i;
        while (1 = 1) {
            let key = Keyboard.keyPressed();
            if (key = 132) {
                do moveRight();
                do Sys.wait(100);
            }
            if (key = 130) {
                    do moveLeft();
                    do Sys.wait(100);
            }
            if (key = 131) {
                    do rotate();
                    do Sys.wait(100);
            }
            if (key = 133) {
                while ((Keyboard.keyPressed() = 133) & (moveDown() = 0)) {
                    do Sys.wait(7);
                }
            }
            if (key = 32) {
                while (moveDown() = 0) {
                }
                while (~(Keyboard.keyPressed() = 0)) {
                }
            }           

            if (moveDown() = 1) {
                while ((i < 20) & ((Keyboard.keyPressed() = 130) | (Keyboard.keyPressed() = 131) | (Keyboard.keyPressed() = 132))) {
                    if (Keyboard.keyPressed() = 132) {
                        do moveRight();
                        do Sys.wait(100);
                    }
                    if (Keyboard.keyPressed() = 130) {
                            do moveLeft();
                            do Sys.wait(100);
                    }
                    if (Keyboard.keyPressed() = 131) {
                            do rotate();
                            do Sys.wait(100);
                    }
                    let i = i + 1;
                }
            
                if (moveDown() = 1) {
                    do instance.addSquare(a.getColumn(), a.getRow());
                    do instance.addSquare(b.getColumn(), b.getRow());
                    do instance.addSquare(c.getColumn(), c.getRow());
                    do instance.addSquare(d.getColumn(), d.getRow());

                    do instance.linesDown(a.getRow(), b.getRow(), c.getRow(), d.getRow());

                    return 1;
                }
            }

            do Sys.wait(wait);

        }
        
        return 0;
    }

    /** Moves right by one square if there is no obstacle */
    method void moveRight() {
        if ((b.getX() = rightWall) | (d.getX() = rightWall)) {
            return;
        }
        if (instance.getRight(b.getColumn(), b.getRow(), 2, 0, counter) = 1) {
            return;
        }

        do a.erase();
        do b.erase();
        do c.erase();
        do d.erase();

        do a.setLoc(a.getX() + size, a.getY());
        do b.setLoc(b.getX() + size, b.getY());
        do c.setLoc(c.getX() + size, c.getY());
        do d.setLoc(d.getX() + size, d.getY());

        do a.draw();
        do b.draw();
        do c.draw();
        do d.draw();

        do a.updateColumn(1);
        do b.updateColumn(1);
        do c.updateColumn(1);
        do d.updateColumn(1);

        return;
    }

    /** Moves left by one square if there is no obstacle */
    method void moveLeft() {
        if ((a.getX() = leftWall) | (c.getX() = leftWall)) {
            return;
        }

        if (instance.getLeft(a.getColumn(), a.getRow(), 2, 0, counter) = 1) {
            return;
        }

        do a.erase();
        do b.erase();
        do c.erase();
        do d.erase();

        do a.setLoc(a.getX() - size, a.getY());
        do b.setLoc(b.getX() - size, b.getY());
        do c.setLoc(c.getX() - size, c.getY());
        do d.setLoc(d.getX() - size, d.getY());

        do a.draw();
        do b.draw();
        do c.draw();
        do d.draw();

        do a.updateColumn(-1);
        do b.updateColumn(-1);
        do c.updateColumn(-1);
        do d.updateColumn(-1);
        
        return;
    }

    /** Moves down by 2 pixels if there is no obstacle, else returns 1. */
    method int moveDown() {
        if ((c.getY() = bottom) | (d.getY() = bottom)) {
            return 1;
        }

        if (instance.getDown(c.getColumn(), c.getRow(), 2, 0) = 1) {
            return 1;
        }

        do a.erase();
        do b.erase();
        do c.erase();
        do d.erase();

        do a.setLoc(a.getX(), a.getY() + 2);
        do b.setLoc(b.getX(), b.getY() + 2);
        do c.setLoc(c.getX(), c.getY() + 2);
        do d.setLoc(d.getX(), d.getY() + 2);

        do a.draw();
        do b.draw();
        do c.draw();
        do d.draw();

        if (counter = (size - 2)) {
            do a.updateRow(1);
            do b.updateRow(1);
            do c.updateRow(1);
            do d.updateRow(1);
            let counter = 0;
        }
        else {
            let counter = counter + 2;

        }
        return 0;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do a.dispose();
        do b.dispose();
        do c.dispose();
        do d.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Since a square has rotational symmetry it just erases each equare and prints them again. */
    method void rotate() {
        do a.erase();
        do b.erase();
        do c.erase();
        do d.erase();

        do a.draw();
        do b.draw();
        do c.draw();
        do d.draw();
        return;
    }

    /** Returns 1 if there is already a square in any of the position of the new figure. */
    method int checkLose() {
        if ((instance.getIndex(a.getColumn(), a.getRow()) = 1) | 
            (instance.getIndex(b.getColumn(), b.getRow()) = 1) |
            (instance.getIndex(c.getColumn(), c.getRow()) = 1) |
            (instance.getIndex(d.getColumn(), d.getRow()) = 1)) {
            do shape.dispose();
            return 1;
        }
        return 0;
    }  
}

